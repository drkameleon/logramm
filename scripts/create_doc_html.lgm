import default;

__Args.count() == 0 : panic "Not enough arguments!";

dest 		= __Args[0];
destFile 	= "{:dest}/library.html";

ret = "";
ret += "<html>\n\t<head>\n\t\t<title>Logramm - Library Documentation</title>\n\t</head>";
ret += "\n\t<body>\n\t\t<h1>Logramm - Library Documentation</h1>";

methods = methods();
currentModule = "";

skipNext = false;

@ method -> methods :
{
	skipNext == true : skipNext=false;
	else :
	{
		module 		= method.split("::")[0];
		function 	= method.split("::")[1];

		module != currentModule : 
		{
			currentModule != "" : ret += "\n\t\t</div>";

			// Initiate new section
			currentModule = module;
			ret += "\n\t\t<div class=\"section\">";
			ret += "\n\t\t\t<hr/><h2>{:currentModule}</h2><hr/>\n";
		}

		infos	 	= module.methodInfo(function);
		infos.count() > 1 : skipNext = true ;

		j = 0;
		
		@ j < infos.count() :
		{
			info = infos[j];

			ret += "\t\t\t<h4>" + function + " (";

			i=0;
			@ i<info.args.count() :
			{
				ret += "\n\t\t\t\t" + info.args[i] + ":<span style='color:#999;'>" + info.params[i] + "</span>";

				i < info.args.count()-1 : ret += ", ";

				i += 1;
			}

			ret += "\n\t\t\t) --&gt; " + info.returns + "</h4>\n";
			ret += "\t\t\t<p>" + info.help + "</p>\n";

			ret += "\t\t\t<br>\n";

			j += 1;
		}
	}
}

ret += "\n\t\t</div>";

ret += "\n\t</body>\n</html>";

destFile.write(ret);

