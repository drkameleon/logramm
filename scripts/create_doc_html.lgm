import default;

__Args.count() == 0 : panic "Not enough arguments!";

dest 		= __Args[0];
destFile 	= "{:dest}/library.html";

ret = "";
ret += "<html><head><title>Logramm - Library Documentation</title></head>";
ret += "<body><h1>Logramm - Library Documentation</h1>";

methods = methods();
currentModule = "";

skipNext = false;

@ method -> methods :
{
	skipNext == true : skipNext=false;
	else :
	{
		module 		= method.split("::")[0];
		function 	= method.split("::")[1];

		module != currentModule : 
		{
			// Initiate new section
			currentModule = module;

			ret += "<hr/><h2>{:currentModule}</h2><hr/>\n";
		}

		infos	 	= module.methodInfo(function);
		infos.count() > 1 : skipNext = true ;

		j = 0;
		
		@ j < infos.count() :
		{
			info = infos[j];

			ret += "<h4>" + function + " (";

			i=0;
			@ i<info.args.count() :
			{
				ret += info.args[i] + ":<span style='color:#999;'>" + info.params[i] + "</span>\n";

				i < info.args.count()-1 : ret += ", ";

				i += 1;
			}

			ret += ") --> " + info.returns + "</h4>\n";
			ret += "<p>" + info.help + "</p>\n";

			ret += "<br>\n";

			j += 1;
		}
	}
}

ret += "</body></html>";

destFile.write(ret);