/**********************************************************
 **
 ** LOGRAMM
 ** Interpreter
 ** 
 ** (c) 2009-2014, Dr.Kameleon
 **
 **********************************************************
 ** lib/core/reflection.lgm
 **********************************************************/

//================================================
// Imported modules
//================================================

import core;

//================================================
// Module definition
//================================================

Reflection :: 
{

	//================================================
	// External D-bindings
	//================================================

	external symbolExists(symbol) # [
		.params		: "string",
		.returns 	: "boolean",
		.help		: "Checks if the given symbol/variable is defined"
	] => "reflection.symbolExists";

	external methodInfo(func) # [
		.params		: "string",
		.returns	: "dictionary",
		.help		: "Returns info for the given method or false if not found"
	] => "reflection.functionInfo";

	external methodExists(func) # [
		.params		: "string",
		.returns 	: "boolean",
		.help		: "Checks if the given method is defined"
	] => "reflection.functionExists";

	external methodExists(mod,func) # [
		.params		: "string,string",
		.returns 	: "boolean",
		.help		: "Checks if the given method, in module 'mod', is defined"
	] => "reflection.functionExistsInModule";

	external inspect(symbol) # [
		.params		: "any",
		.returns	: "no",
		.help		: "Prints debugging information related to given symbol"
	] => "reflection.inspect";

	external symbols() # [
		.params		: "",
		.returns 	: "array",
		.help		: "Returns an array with defined symbols"
	] => "reflection.symbols";

	external methods() # [
		.params		: "",
		.returns 	: "array",
		.help		: "Returns an array with defined methods"
	] => "reflection.methods";

	//================================================
	// Functions
	//================================================

	callMethod(func) # [
		.params		: "string",
		.returns 	: "boolean",
		.help		: "Executes method by name 'func' and returns true if successful"
	] =>
	{
		methodExists(func) :
		methodInfo(func).params.count() == 0 : { exec "{:func}();"; ==> true; }
		
		==> false;
	}

	callMethod(mod,func) # [
		.params		: "string,string",
		.returns 	: "boolean",
		.help		: "Executes method by name 'func', in module 'mod', and returns true if successful"
	] =>
	{
		methodExists(mod,func) :
		methodInfo(func).params.count() == 0 : { exec "{:mod}::{:func}();"; ==> true; }
		
		==> false;
	}

	callMethodWithArgs(func,args) # [
		.params		: "string,array",
		.returns 	: "boolean",
		.help		: "Executes method by name 'func', using arguments, and returns true if successful"
	] =>
	{
		argstr = args.join(",");

		methodExists(func) :
		methodInfo(func).params.count() == args.count() : { exec "{:func}({:argstr});"; ==> true; }
		
		==> false;
	}

	callMethodWithArgs(mod,func,args) # [
		.params		: "string,string,array",
		.returns 	: "boolean",
		.help		: "Executes method by name 'func', in module 'mod', using arguments, and returns true if successful"
	] =>
	{
		argstr = args.join(",");

		methodExists(mod,func) :
		methodInfo(func).params.count() == args.count() : { exec "{:mod}::{:func}({:argstr});"; ==> true; }
		
		==> false;
	}

	importPackage(pkg) # [
		.params		: "string",
		.returns 	: "no",
		.help		: "Programmatically import package by string name 'pkg'"
	] =>
	{
		exec "import {:pkg};";
	}

}
